/*
 // Example Call to script:
 Set vPath = '$(vFolder_DWH)';                  // Folder to GET and to SAVE
 Set vFile = 'Filename';                        // ".qvd" is not necessary
 Call MetaFile('vMeta_Path', 'vMeta_Filename');
 Drop Table [METADATA_$(vMeta_Filename)];       // After use
*/

Sub MetaFile(vMeta_Path, vMeta_Filename)


 // Debug
 Trace 
 .
 -------------------------------------------------------------
  > Reading Metadata from table: $(vMeta_Path)/$(vMeta_Filename)
 -------------------------------------------------------------;

 STORE [$(vMeta_Filename)] into $(vMeta_Path)/$(vMeta_Filename).qvd;

 [TMP File]:
 Load
  String%Table                                                                                             as Tags,
  FileBaseName() &'|'& %Key_QvdFieldHeader_59D66ED49CFF179D                                                as KeyField
 From '$(vMeta_Path)'/'$(vMeta_Filename)'.qvd (xmlSimple, Table is [QvdTableHeader/Fields/QvdFieldHeader/Tags/String]);


 [MAP Meta $(vMeta_Filename)]:
 Mapping Load
  KeyField,
  Concat(Tags,', ')                                                                                        as Tags
 Resident [TMP File]
  Group By KeyField;

 Drop Table [TMP File];


 [TMP META]:
 Load 
  FileBaseName() &'|'& %Key_QvdTableHeader_B94FCCAC68ED3E20                                                as KeyTable,
  FileBaseName() &'|'& FieldName                                                                           as KeyField,
  FieldName,
  ApplyMap('MAP Meta $(vMeta_Filename)', FileBaseName() &'|'& %Key_QvdFieldHeader_59D66ED49CFF179D,'NONE') as Tags,
  BitOffset,
  BitWidth,
  Bias,
  NoOfSymbols,
  Offset,
  Length,
  [NumberFormat/Type]                                                                                      as Type,
  [NumberFormat/nDec]                                                                                      as nDec,
  [NumberFormat/UseThou]                                                                                   as UseThou,
  [NumberFormat/Fmt]                                                                                       as Fmt,
  [NumberFormat/Dec]                                                                                       as Dec,
  [NumberFormat/Thou]                                                                                      as Thou
 From '$(vMeta_Path)'/'$(vMeta_Filename)'.qvd (xmlSimple, Table is [QvdTableHeader/Fields/QvdFieldHeader]);


 // Sort order:
 NoConcatenate
 [METADATA_$(vMeta_Filename)]:
 Load
  *
 Resident [TMP META]
  Order By FieldName;

 Drop Table [TMP META];


 Store [METADATA_$(vMeta_Filename)] into $(vMeta_Path)/Meta_$(vMeta_Filename).qvd(qvd);

 Drop Table [METADATA_$(vMeta_Filename)];


 // Debug
 Trace 
 .
 -------------------------------------------------------------
  > Ending Metadata Generator
 -------------------------------------------------------------;


End Sub // Sub MetaFile(vMeta_Path, vMeta_Filename)